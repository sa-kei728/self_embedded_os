OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
    ramall(rwx) : o = 0xffbf20, l = 0x004000    /* 16KB */
    softvec(rw) : o = 0xffbf20, l = 0x000040    /* top of RAM */
    ram(rwx)    : o = 0xffc020, l = 0x003f00
    stack(rw)   : o = 0xffff00, l = 0x000000    /* end of RAM */
}

SECTIONS
{
    .text : {
        _text_start = . ;
        *(.text)
        _etext = . ;
    } > ram

    .rodata : {
        _rodata_start = . ;
        *(.string)
        *(.rodata)
        *(.rodata.*)
        _erodata = . ;
    } > ram

    .softvec : {
        _softvec = .;
    } > softvec

    .data : {
        _data_start = . ;
        *(.data)
        _edata = . ;
    } > ram

    .bss : {
        _bss_start = . ;
        *(.bss)
        *(COMMON)
        _ebss = . ;
    } > ram

    . = ALIGN(4);
    _end = . ;

    .stack : {
        _stack = . ;
    } > stack
}
